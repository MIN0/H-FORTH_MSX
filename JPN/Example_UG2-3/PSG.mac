
	ASEG
	ORG	0100H

XRLID:	DB	'XR'
ROFS:	DW	ORGLOC-0100H	; 0100H番地からのオフセット

; －－－　ここからボディ、すなわちマシン語ルーチンの本体　－－－

XRLBDY:			; 別にこんなラベルつけなくても良いのですが(^^;

REGSEL:	DB	0	; Ｈ－ＦＯＲＴＨとデータのやり取りを行う領域
REGDAT:	DB	0	; 同上

RDPSG:
RL0001:	JP	XRDPSG	; Ｈ－ＦＯＲＴＨからのコールの入り口
WRPSG:
RL0002:	JP	XWRPSG	; 同上

; 以上のように、データをやり取りする領域と、コールの入り口はボディの最初の方
; にまとめて置くのが賢明です。マシン語ルーチンのバージョンが変わってもＨ－Ｆ
; ＯＲＴＨ側のプログラムは直さずに済みます。

XRDPSG:
RL0003:	LD	A,(REGSEL)	; レジスタ番号
	LD	IX,096H		; ＢＩＯＳのＲＤＰＳＧの番地
	LD	IY,(0FCC0H)	; メインＲＯＭのスロット
	CALL	01CH		; インタースロットコール
RL0004:	LD	(REGDAT),A	; 結果
	RET

XWRPSG:
RL0005:	LD	A,(REGDAT)	; 書き込むデータ
	LD	E,A
RL0006:	LD	A,(REGSEL)	; レジスタ番号
	LD	IX,093H		; ＢＩＯＳのＷＲＴＰＳＧの番地
	LD	IY,(0FCC0H)	; メインＲＯＭのスロット
	CALL	01CH		; インタースロットコール
	RET

; －－－　ここからがリロケーションテーブル　－－－

ORGLOC:	DW	0100H		; 本来のアドレス、これも 0100H が起点

RLCENT:	DW	RL0001+1	; リロケーションエントリです。
	DW	RL0002+1	; リロケートすべき番地を列挙します。
	DW	RL0003+1	; ジャンプ、ロード命令などのオペランド
	DW	RL0004+1	; 部分の番地です。
	DW	RL0005+1
	DW	RL0006+1
ENDRLC:	DW	0		; リロケーションテーブルの終わりです。
				; 0 が終わりを表しますからリロケーション
				; エントリの中に 0 は使えません。

	END

