
ARRAY( BYTE: MARK 1001 )	/* マーキング用配列 */

/* --- Ｎの倍数のマークを消すワードの定義 --- */

: UNMARK  PARAM( N )		/* ワード名は UNMARK 、パラメータは N */
VAR( I )			/* UNMARK でのみ有効なローカル変数 I */

N 2 * >> I			/* I の初期値は N*2 */
WHILE( I 1000 <= ){		/* I<=1000 の間くり返し */
    FALSE >> MARK [ I ]		/* I番のマークを FALSE （０）にする */
    I N + >> I			/* I に N を足す */
}				/* ループここまで */
;				/* ワード UNMARK の定義終わり */

/* --- メインワードの定義 --- */

: MAIN				/* ワード名は MAIN 、パラメータなし */
VAR( I )			/* MAIN でのみ有効なローカル変数 I */

"\n１から１０００まで素数を求めるぞ！！\n" STR.	/* メッセージ表示 */

1 >> I
WHILE( I 1000 <= ){		/* 1〜1000 まで、 */
    TRUE >> MARK [ I ]		/* MARK をすべて TRUE （１）にする */
    I 1 + >> I
}

2 >> I
WHILE( I 1000 <= ){		/* 2〜1000 まで、 */
    I UNMARK			/* ワード UNMARK を呼び出す */
    I 1 + >> I
}

1 >> I
WHILE( I 1000 <= ){		/* 1〜1000 まで */
    MARK [ I ] IF{ I . }	/* MARK が０でなければ I を表示 */
    I 1 + >> I
}
"\nどうだ！！" STR.		/* 終了メッセージ表示 */
;
END MAIN			/* ソースプログラム終わり、メインワード指定 */

